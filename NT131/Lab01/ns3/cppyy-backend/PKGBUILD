# Maintainer: Renato Lui Geh <renatogeh at gmail dot com>

_realname=cppyy-backend
pkgname=cppyy-backend-ns3
pkgver=1.14.10
pkgrel=1
pkgdesc="Cling-based Python-C++ bindings (backend for ns3)"
arch=('any')
url="https://cppyy.readthedocs.io/en/latest/index.html"
license=('LBNL BSD')
provides=('cppyy-backend')
conflicts=('cppyy-backend')
depends=('python' 'cppyy-cling-ns3')
makedepends=('python-build' 'python-installer' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('51ac01c4cca28ef54544ce0b41355d885242f1a0178619bea071f5c43fd03dac')

build() {
  cd "$_realname-$pkgver"
  CFLAGS="$CFLAGS -DSTRLCPY" python -m build --wheel --no-isolation
}

package() {
  cd "$_realname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
